//file:noinspection GrUnresolvedAccess
//file:noinspection GroovyAssignabilityCheck

configurations {
    tests
    testArtifacts
    mockitoAgent
}

dependencies {
    mockitoAgent 'org.mockito:mockito-core:' + testLibDependencies.mockitoMockitoCoreVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-api:' + testLibDependencies.junitJupiterJunitJupiterApiVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-params:' + testLibDependencies.junitJupiterJunitJupiterParamsVersion
    testImplementation 'org.mockito:mockito-core:' + testLibDependencies.mockitoMockitoCoreVersion
    testImplementation 'org.mockito:mockito-junit-jupiter:' + testLibDependencies.mockitoMockitoJunitJupiterVersion
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + testLibDependencies.junitJupiterJunitJupiterEngineVersion
}

test {
    useJUnitPlatform()
    jvmArgs("-javaagent:${configurations.mockitoAgent.resolvedConfiguration.resolvedArtifacts[0].file}")
    ignoreFailures = true
    failOnNoDiscoveredTests = false
    reports {
        junitXml.required = true
        html.required = false
    }
    exclude '**/*IntegrationTest*'
}

tasks.register('testJar', Jar) {
    dependsOn testClasses
    archiveBaseName.set("${moduleName}")
    archiveClassifier.set("test")
    from sourceSets.test.output
}

jar.finalizedBy testJar

artifacts {
    testArtifacts testJar
}